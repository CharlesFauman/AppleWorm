plugins{
    id 'java'
    id 'application'
    id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "org.fauman.appleworm.Main"

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.processing', name: 'core', version: '3.3.6'
    implementation group: 'net.compartmental.code', name: 'minim', version: '2.2.2'
    implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            groupId = 'org.fauman'
            artifactId = 'appleworm'
            version = '1.0.1'

            pom {
                name = 'Appleworm'
                description = 'A java clone of AppleWorm from coolmathgames.com'
                url = 'https://github.com/CharlesFauman/AppleWorm'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'CharlesFauman'
                        name = 'Charles Fauman'
                        email = 'CharlesFauman@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:svn:https://github.com/CharlesFauman/AppleWorm.git'
                    developerConnection = 'scm:git@github.com:CharlesFauman/AppleWorm.git'
                    url = 'https://github.com/CharlesFauman/AppleWorm'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/CharlesFauman/AppleWorm"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
